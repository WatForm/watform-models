open util/ordering[PID] as P0

state Counter {
	env event Tk0 {}
	event Done {}
	current: one PID

	conc Bit [PID] {
		event Tk1 {}

		default state Bit1 {}
		state Bit2 {}

		trans currentBitToBit2{
			from Bit1
			when this in current
			on Tk0
			goto Bit2
		}

		trans currentBitToBit1 {
			from Bit2
			on Tk0
			when this in current
			goto Bit1
			send Bit[P0/next[this]]/Tk1
		}

		trans nextBitToBit2 {
 			from Bit1
			when this in P0/next[current]
			on Tk1
			goto Bit2
		}

		trans nextBitToBit1 {
			from Bit2
			when {
				 this in P0/next[current]
				 P0/next[current] !in P0/last
			}
			on Tk1
			do current' = P0/next[current]
			goto Bit1
		}

		trans lastBitDone {
			from Bit2
			when this in P0/last
			on Tk1
			goto Bit1
			send Done
		}

		init {
			current = P0/first
		}
	}
}

// remember to have TCMC option set 

assert ctl_allBitsInBit0 {
        ctl_mc[af[{ s: Snapshot | Counter_Done in s.events0 and 
		(all p: PID | Counter_Bit_Bit1 in p.(s.conf1)) }]]
}

check ctl_allBitsInBit0 for 12 Snapshot, exactly 3 PID, 1 PathNode
check ctl_allBitsInBit0 for 20 Snapshot, exactly 4 PID, 1 PathNode
