open util/ordering[Temp] as temp

sig Temp{}
abstract sig ValvePos {}
one sig OPEN, HALF, CLOSED extends ValvePos {}

state HeatingSystem {
	env event Reset {}
	env event TurnOn {}
	env event furnaceFault {}
	env event userReset {}
	env event heatSwitchOn {}

	event activate {}
	event deactivate {}
	event furnaceRunning {}
	event furnaceNotRunning {}
        event furnaceReset {}

	default state Functioning {
		conc Furnace {
			default state Furnace_Normal {
				default state Furnace_Off {
					trans T1 {
						on activate
						goto Furnace_Activating
					}
				}
				state Furnace_Activating {
					trans T2 {
						on deactivate
						goto Furnace_Off
					}
					trans T3 {
						send furnaceRunning
						goto Furnace_Running 
					}
				}
				state Furnace_Running {
					trans T4 {
						on deactivate
						goto Furnace_Off					
					}
					trans T5 {
						on furnaceFault
						goto HeatingSystem/ERROR
					}
				}
			}	
		}
	
		conc Controller {
			controllerOn: one Bool
			//condition heatRequested [some r: Identifier | Functioning/Room[r]/requestHeat = True] {}
			//condition noHeatRequested [no r: Identifier |  Functioning/Room[r]/requestHeat = True] {}

			default state Off {
				trans T8 {
					on heatSwitchOn
					send furnaceReset
					do controllerOn' = True
					goto On
				}
			}
		
			state On {
				default state Idle {
					trans T9 {
						when { some r: Identifier | Functioning/Room[r]/requestHeat = True }
						send activate
						goto Heater_Active
					}
				}
				state Heater_Active {
					trans T10 {
						when { no r: Identifier |  Functioning/Room[r]/requestHeat = True }
						send deactivate
						goto Idle
					}
					trans T11 {
						on furnaceFault
						do controllerOn' = False
						goto HeatingSystem/ERROR
					}
				}
			}

			init {
				controllerOn = False
			}	
		}
	
		conc Room [Identifier] {
			actualTemp: one Temp
			desiredTemp: one Temp
			valvePos: one ValvePos
			requestHeat: one Bool
	
			env event waitedForWarmth {}
			env event waitedForCool {}
	
			default state No_Heat_Request {
				default state Idle_No_Heat {
					trans T12 {
						when lt[actualTemp, desiredTemp]
						goto Wait_For_Heat
					}
					trans coolRoom {
						when !lt[actualTemp, desiredTemp]
						do actualTemp' = temp/prev[actualTemp]
					}
				}
				state Wait_For_Heat {
					trans T13 {
						when !(lt[actualTemp, desiredTemp])
						goto Idle_No_Heat
					}
					trans T14 {
						on waitedForWarmth
						when valvePos = CLOSED
						do valvePos' = OPEN
					}
					trans T15 {
						when valvePos = OPEN and Functioning/Controller/controllerOn = True
						do requestHeat' = True
						goto Heat_Requested
					}
				}
			}
			
			state Heat_Requested {
				default state Idle_Heating {
					trans T15 {
						when gt[actualTemp, desiredTemp]
						do valvePos' = CLOSED
						goto Wait_For_Cool
					}
					trans heatRoom {
						when !(gt[actualTemp, desiredTemp])
						do actualTemp' = temp/next[actualTemp]
					}
				}
	
				state Wait_For_Cool {
					trans T16 {
						when !(gt[actualTemp, desiredTemp])
						goto Idle_Heating
					}
					trans T17 {
						on waitedForCool
						do valvePos' = CLOSED
					}
					trans T18 {
						on waitedForCool
						when valvePos = CLOSED
						do {
							requestHeat' = False
							actualTemp' = desiredTemp
						}
						goto No_Heat_Request
					}
				}
			}
			
			init {
				requestHeat = False
				valvePos = CLOSED
			}
		}
	}

	state ERROR {
		trans T19 {
			on heatSwitchOn
			goto Functioning
		}
	}
}

