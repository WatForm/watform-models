open util/ring[Node] as node

abstract sig Status {}
one sig Stabilizing, Rectifying extends Status{}

pred between [n1, nb, n2: Node] {
	     lt[n1,n2] =>   ( lt[n1,nb] && lt[nb,n2] )
             else ( lt[n1,nb] || lt[nb,n2] )  }

// Returns the first node if current node is the last Node in the ordering
fun nextNode [n: Node] : lone Node  {
	{m: Node | no node/next[n] implies m = node/first
		else m = node/next[n]
	}
}

// Returns the previous node in the ordering
fun prevNode [n: Node] : lone Node  {
	{m: Node | no node/prev[n] implies m = node/last
		else m = node/prev[n]
	}
}

conc state System {
	members: Node

	conc state N [Node] {
		frst: lone Node
		scnd: lone Node
		prdc: lone Node
		status: Status
		saved: lone Node

		default state Live {
			trans Fail {
				when {
					// We cannot fail if it would leave a member with no successors
					all n: Node - this | some (members - this) & (Node[n]/frst + Node[n]/scnd)
				}
				do {
					members' = members - this
					frst' = {none}
					scnd' = {none}
					prdc' = {none}
					status' = {none}
				}
				goto Failed
			}

			trans StabilizeFromSucc {
				when {
					no status
				}
				do {
					// The Sucessor is dead
					frst !in members implies {
						frst' = scnd
						scnd' = nextNode[scnd]
						all n: Node | N[n]/status' = N[n]/status
						all n: Node | N[n]/saved' = N[n]/saved
					// The sucessor is a member
					} else {
						frst' = frst 
						scnd' = N[frst]/frst
						// The sucessor's predecessor is better
						(some N[frst]/prdc and between[this, N[frst]/prdc, frst] and (N[frst]/prdc in members)) => {
							status' = Stabilizing
							saved' = N[frst]/prdc
							all n: Node - this | N[n]/status' = N[n]/status
							all n: Node - this | N[n]/saved' = N[n]/saved
						} else (this !in N[frst]/prdc) => {
							N[frst]/status' = Rectifying
							N[frst]/saved' = this
							all n: Node - frst | N[n]/status' = N[n]/status
							all n: Node - frst | N[n]/saved' = N[n]/saved
						} else {
							all n: Node | N[n]/status' = N[n]/status
							all n: Node | N[n]/saved' = N[n]/saved
						}
					}
				}
			}

			trans StabilizeFromPrdc {
				when {
					this in members
					status = Stabilizing
					between[this, saved, frst]
				}
				do {
					no status'
					no saved'
					// The Successor's Predecessor is dead
					saved !in members implies {
						frst' = frst
						scnd' = scnd
						all n: Node - this | N[n]/status' = N[n]/status
						all n: Node - this | N[n]/saved' = N[n]/saved
					// The Successor's Predecessor is a member
					} else {
						frst' = saved
						scnd' = N[saved]/frst
						N[saved]/status' = Rectifying
						N[saved]/saved' = this
						all n: Node - saved - this | N[n]/status' = N[n]/status
						all n: Node - saved - this | N[n]/saved' = N[n]/saved
					}
				}
			}

			trans Rectify {
				when {
					this in members
					status = Rectifying
				}
				do {
					(between[prdc, saved, this] or prdc !in members or no prdc) => {prdc' = saved}
					else {
						prdc' = prdc
					}
				}
			}

			trans Flush {
				when {
					this in members
					prdc !in members
				}
				do {
					prdc' = {none}
				}
			}
		}

		state Failed {
			trans Join {
				when {
					Node in members
				}
				do {
					members' = members + this
				}
				goto Live
			}
		}

		init {
			no status
			no saved
			frst = nextNode[this]
			scnd = nextNode[frst]
			prdc = prevNode[this]
		}
	}

	init {
		// All Nodes are members initially
		Node in members
	}
}


fact alwaysThreeMembers {
	#(System_members) >= 3
}

//fact {
//	one n: Node | (System_N_Failed) in n.(snapshot/next[snapshot/first].(Snapshot <: conf1))
//}


/******************************************* FUNCTIONS FOR CHECKING PROPERTIES ******************************************/

// Returns a tuple (Node, Node) where the second element is a Live successor of the first element
fun succ : Node -> Node {
	{ m1, m2: Variables.System_members | m1.(System_N_frst) in Variables.System_members => (m2 = m1.(System_N_frst)) else (m2 = m1.(System_N_scnd)) }
}

// Returns the members that form a ring
fun ring : some Node { {m: Variables.System_members | m in m.^(succ)} }

fun appendages: set Node { Variables.System_members - ring }

/******************************************* PROPERTIES ******************************************/

// Members form atleast one ring
pred atLeastOneRing  {
	some ring
}

// Members form atmost one ring
pred atMostOneRing {
	(all m1, m2: ring| m1 in m2.^(succ))
}

// Members are ordered
pred orderedRing { 
	all disj m1, m2, mb: ring |
		m2 = m1.^(succ) implies not between[m1, mb, m2] 
}

// Member successors are ordered
pred orderedSuccessors {
	(all m: Variables.System_members | between[m, m.(System_N_frst), m.(System_N_scnd)])
}

pred connectedAppendages {
	(all m1: appendages | some m2: ring | m2 in m1.^(succ))
}

pred valid {
	atLeastOneRing
	atMostOneRing
	orderedSuccessors
	connectedAppendages
	orderedRing
}

assert idealAssert {
	eventually (always (System_N_Failed_Join + System_N_Live_Fail) !in Identifiers.taken1) implies (eventually always ideal)
}

pred ideal {
	valid
	no appendages
	(System_N_frst) = ~(System_N_prdc)
}

check idealAssert for exactly 4 Node
check idealAssert for exactly 5 Node
